plugins {
	id 'org.springframework.boot' version '3.2.4'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'java'
	id 'com.github.ben-manes.versions' version '0.50.0'
	id "org.ec4j.editorconfig" version "0.0.3"
}

group = 'com.glinboy'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	modelMapperVersion = '3.2.0'
	riderJunitVersion = '1.41.0'
	openapiVersion = '2.3.0'
}

defaultTasks "bootRun"

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation('org.springframework.boot:spring-boot-starter-web') {
		exclude module: 'spring-boot-starter-tomcat'
	}
	implementation "org.springframework.boot:spring-boot-starter-undertow"
	implementation 'org.flywaydb:flyway-core'
	implementation "org.modelmapper:modelmapper:${modelMapperVersion}"
	implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${openapiVersion}"
	implementation 'org.hibernate.validator:hibernate-validator'
	compileOnly "org.projectlombok:lombok"
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor "org.projectlombok:lombok"
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation "com.github.database-rider:rider-junit5:${riderJunitVersion}"
}

tasks.withType(JavaCompile) {
	options.compilerArgs = [
		'-Amapstruct.suppressGeneratorTimestamp=true'
	]
}

bootRun {
	jvmArgs = ['-XX:+UseZGC',
				'-Xlog:gc*:file=log/linkshorter.gc.log',
				'-XX:+HeapDumpOnOutOfMemoryError',
				'-XX:HeapDumpPath=log/crashes/linkshorter.hprof',
				'-Xss256k',
				'-Dsun.net.client.defaultConnectTimeout=2000',
				'-Dsun.net.client.defaultReadTimeout=2000'
			]
}

test {
	systemProperty 'spring.profiles.active', 'test'
	testLogging.showStandardStreams = true
	useJUnitPlatform()
}
